## Could combine with compute-bid-difference if that used new unknown computation.
## Only consider pushing after first bid

sp {evaluate-operator*propose*compute-bid-push-probability
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^top-state.io.input-link.state <state>)
   (<op> ^type compute-probability
        ^superoperator <so>)
   (<so> ^name bid
         ^push-face-count
         ^face <face>
         ^multiplier <multipler>)
   (<state> -^last-bid nil)
-->
   (<op> ^name compute-bid-push-probability
         ^face <face>
         ^multiplier <multipler>)
}

#### MUST COUNT NUMBER OF DICE/FACE and use that for <push-count> below.

## Add in dice that are rerolled into the unknown for used in computing bid difference

sp {evaluate-operator*elaborate*compute-bid-push-probability*1*push*unknown
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs> 
              ^top-state.io.input-link.players.me.cup <cup>
              ^superstate.operator.superoperator <so>)
   (<dcs> ^dice-count <dc2>)
   (<cup> ^count <undercup>
          ^<push-face> { < <undercup> <push-count> })
   (<op> ^name compute-bid-push-probability)
   (<so> ^name bid
         ^push-face-count 1
         ^push-face <push-face>)
   (<dc2> ^face unknown
          ^count <unknown>)
-->
   (write (crlf) |Unknowns because of single push (| <push-face> |) = |  (+ <unknown> (- <undercup> <push-count>)))
   (write (crlf) |   Original unknowns: | <unknown> | + undercup | <undercup> | - push-count | <push-count> )
   (<op> ^unknown (+ <unknown> (- <undercup> <push-count>)))
}


sp {evaluate-operator*elaborate*compute-bid-push-probability*1*push*unknown*allsame
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs>
              ^top-state.io.input-link.players.me.cup <cup>
              ^superstate.operator.superoperator <so>)
    (<dcs> ^dice-count <dc2>)
   (<cup> ^count <undercup>
          ^<push-face> <undercup>)  # Everything undercup is <push-face>
   (<op> ^name compute-bid-push-probability)
   (<so> ^name bid
         ^push-face-count 1
         ^push-face <push-face>)
   (<dc2> ^face unknown
          ^count <unknown>)
-->
   (write (crlf) |Unknowns because of single push (| <push-face> |) = |  (+ <unknown> 1))
   (write (crlf) |   Original unknowns: | <unknown> )
   (<op> ^unknown (+ <unknown> 1))
}

## When pushing 1's and another face

sp {evaluate-operator*elaborate*compute-bid-push-probability*2*push*unknown
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs>
              ^top-state.io.output-link <out>
              ^top-state.io.input-link.players.me.cup <cup>
              ^superstate.operator.superoperator <so>)
   (<dcs> ^dice-count <dc3>)
   (<cup> ^count <undercup>
          ^<push-face> <push-count>
          ^1 <push-count1>)
   (<op> ^name << compute-bid-push-probability compute-bid-probability >>)
   (<so> ^name bid
         ^push-face-count 2
         ^push-face { <> 1 <push-face> }
         ^push-face 1)
   (<dc3> ^face unknown
          ^count <unknown>)
-->
   (write (crlf) |Unknowns because of double push (| <push-face> |, 1) = | (+ <unknown> (- <undercup> (+ <push-count1> <push-count>))))
   (write (crlf) |   Original unknowns: | <unknown> | + undercup | <undercup> | - push-count(face) | <push-count> | - push-count(1) | <push-count1> )
   (<op> ^unknown (+ <unknown> (- <undercup> (+ <push-count1> <push-count>))))
}

### SPECIALS - After Model 
##################################

## No special
sp {evaluate-operator*elaborate*compute-bid-push-probability*model*no-model
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs> )
   (<op> ^name << compute-bid-push-probability compute-bid-probability >>)
  -(<dcs> ^dice-count.face special)
-->
   (write (crlf) |No Model.|)
   (<op> ^sunknown 0)
}

## Different face for pushing one face.
sp {evaluate-operator*elaborate*compute-bid-push-probability*model*same
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs> 
              ^superstate.operator.superoperator <so>)
   (<dcs> ^dice-count <dc>)
   (<op> ^name << compute-bid-push-probability compute-bid-probability >>)
   (<so> ^name bid
         ^face <face>)
   (<dc> ^face special
         ^not-face <face>)
-->
   (write (crlf) |Model: no additional unknowns because prior bid is same as currrent.|)
   (<op> ^sunknown 0)
}

sp {evaluate-operator*elaborate*compute-bid-push-probability*model*different*prior-not-1
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs> 
              ^superstate.operator.superoperator <so>)
   (<dcs> ^dice-count <dc>)
   (<op> ^name << compute-bid-push-probability compute-bid-probability >>)
   (<so> ^name bid
         ^face <face>)
   (<dc> ^face special
         ^not-face { <> <face> <> 1 }
         ^count <count>)
-->
   (write (crlf) |Model:  additional unknowns because prior bid is different: | <count>)
   (<op> ^sunknown <count>)
}
sp {evaluate-operator*elaborate*compute-bid-push-probability*model*different*prior-1
   (state <s> ^name evaluate-operator
              ^operator <op> +
              ^dice-counts <dcs> 
              ^superstate.operator.superoperator <so>)
   (<dcs> ^dice-count <dc>)
   (<op> ^name << compute-bid-push-probability compute-bid-probability >>)
   (<so> ^name bid
         ^face <face>)
   (<dc> ^face special
         ^not-face { <> <face> 1}
         ^count <count>)
-->
   (write (crlf) |Model:  additional unknowns because prior bid is different - div by 2: count: | <count> | div 2: | (div <count> 2))
   (<op> ^sunknown (div <count> 2))
}

##################################
## Bid 1 with a push.
sp {apply*compute-bid-push-probability*1*bid
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <eval>
              ^dice-counts.dice-count <dc1>)
   (<op> ^name compute-bid-push-probability
         ^face 1
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>
         ^sunknown <sunknown>)
   (<dc1> ^face 1
          ^count <multi-1>)
-->
   (write (crlf) | Bid: | <bid-multi> | 1's. Unknowns: | <multi-unknown> | M-Unknowns: | <sunknown> |, Known 1's: | <multi-1>)
   (write (crlf) |  Count: |  (- <bid-multi> <multi-1>) |, Unknowns: | (+ <sunknown> <multi-unknown>))
   (<s>  ^total-known (- <multi-1> <bid-multi>)
         ^total-possible (- (+ <sunknown> <multi-unknown> <multi-1>) <bid-multi>))
   (<eval> ^compute-probability <cp>)
   (<cp> ^count (- <bid-multi> <multi-1>)
          ^number-of-dice (+ <sunknown> <multi-unknown>)
          ^number-of-faces 6
          ^predicate ge)
}

## Special Rules with a push.
sp {apply*compute-bid-push-probability*special-rules*bid
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <eval>
              ^top-state <ts>
              ^dice-counts.dice-count <dc1>)
   (<op> ^name compute-bid-push-probability
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>
         ^sunknown <sunknown>)
   (<ts> ^io.input-link.state.special true)
   (<dc1> ^face <face>
          ^count <multi-1>)
-->
   (write (crlf) | Bid: | <bid-multi> | 1's. Unknowns: | <multi-unknown> | M-Unknowns: | <sunknown> |, Known | <face> |'s: | <multi-1>  | Special Rules.|)
   (write (crlf) |  Count: |  (- <bid-multi> <multi-1>) |, Unknowns: | (+ <sunknown> <multi-unknown>))
   (<s> ^total-known (- <multi-1> <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <sunknown> <multi-1>) <bid-multi>))
   (<eval> ^compute-probability <cp>)
   (<cp> ^count (- <bid-multi> <multi-1>)
          ^number-of-dice (+ <sunknown> <multi-unknown>)
          ^number-of-faces 6
          ^predicate ge)
}


## NO special rules - not a 1 bid
sp {apply*compute-bid-push-probability*not-special-rules*bid
   (state <s> ^operator <op>
              ^superstate.operator.evaluation <eval>
              ^dice-counts.dice-count <dc1> <dc2>
              ^top-state <ts>)
   (<op> ^name compute-bid-push-probability
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>
         ^sunknown <sunknown>)
   (<ts> ^io.input-link.state.special false)
   (<dc1> ^face <face>
          ^count <multi-bid>)
   (<dc2> ^face 1
          ^count <multi-1>)
-->
   (write (crlf) | Bid: | <bid-multi> | | <face> |'s. Unknowns: | <multi-unknown> | M-Unknowns: | <sunknown> |, Known 1's: | <multi-1>)
   (write (crlf) |  Count: |  (- <bid-multi> (+ <multi-bid> <multi-1>)) |, Unknowns: | (+ <sunknown> <multi-unknown>))
   (<s> ^total-known (- (+ <multi-1> <multi-bid> ) <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <sunknown> <multi-bid> <multi-1>) <bid-multi>))
   (<eval> ^compute-probability <cp>)
   (<cp> ^count (- <bid-multi> (+ <multi-bid> <multi-1>))
          ^number-of-dice  (+ <sunknown> <multi-unknown>)
          ^number-of-faces 3
          ^predicate ge)
}


